# Copyright 1999-2023 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=8

inherit cmake

DESCRIPTION="a C logging library built for high performance and a rich feature set"
HOMEPAGE="http://stumpless.goatshriek.com"
SRC_URI="https://github.com/goatshriek/stumpless/archive/refs/tags/v${PV}.tar.gz -> stumpless-${PV}.tar.gz"

LICENSE="Apache-2.0"
SLOT="0"
KEYWORDS="~amd64 ~x86"
IUSE="doc examples sockets systemd test threads"

DEPEND="test? ( dev-cpp/gtest )
	systemd? ( sys-apps/systemd )"
RDEPEND="${DEPEND}"
BDEPEND=""

# disable test phase if the test USE flag is disabled
RESTRICT="!test? ( test )"

src_configure() {
	local mycmakeargs=(
		-DENABLE_NETWORK_TARGETS="$(usex sockets)"
		-DENABLE_JOURNALD_TARGETS="$(usex systemd)"
		-DENABLE_THREAD_SAFETY="$(usex threads)"
		-DINSTALL_EXAMPLES="$(usex examples)"
		-DINSTALL_MANPAGES="$(usex doc)"
	)
	cmake_src_configure
}

src_test() {
	cmake_build "build-test"

	# journald tests are unable to detect if journald isn't present
	# because of this they fail in a sandbox without journald access
	# so we exclude them from the test suite in the ebuild
	# running the test suite yourself (outside of the ebuild sandbox) should pass
	local myctestargs=( -E journald )
	cmake_src_test
}